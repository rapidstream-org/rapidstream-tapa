"""The cannon test for TAPA."""

# Copyright (c) 2024 RapidStream Design Automation, Inc. and contributors.
# All rights reserved. The contributor(s) of this file has/have agreed to the
# RapidStream Contributor License Agreement.

load("@rules_cc//cc:defs.bzl", "cc_binary")
load("//bazel:tapa_rules.bzl", "tapa_xo")
load("//bazel:v++_rules.bzl", "vpp_xclbin")

exports_files(
    [
        ".",
        "cannon-host.cpp",
        "cannon.cpp",
        "cannon.h",
    ],
    visibility = ["//tests/functional:__subpackages__"],
)

sh_test(
    name = "cannon",
    size = "medium",
    srcs = ["//bazel:v++_env.sh"],
    args = ["$(location cannon-host)"],
    data = [":cannon-host"],
    env = {"TAPA_CONCURRENCY": "1"},
)

sh_test(
    name = "cannon-xosim",
    size = "enormous",
    timeout = "long",  # often >300s
    srcs = ["//bazel:v++_env.sh"],
    args = [
        "$(location cannon-host)",
        "--bitstream=$(location cannon-xo)",
        "--xosim_executable=$(location //tapa/cosim:tapa-fast-cosim)",
    ],
    data = [
        ":cannon-host",
        ":cannon-xo",
        "//tapa/cosim:tapa-fast-cosim",
    ],
    tags = [
        "cpu:2",
    ],
)

sh_test(
    name = "cannon-cosim",
    size = "enormous",
    timeout = "long",  # often >300s
    srcs = ["//bazel:v++_env.sh"],
    args = [
        "$(location cannon-host)",
        "--bitstream=$(location cannon-hw-emu-xclbin)",
    ],
    data = [
        ":cannon-host",
        ":cannon-hw-emu-xclbin",
    ],
    tags = [
        "cpu:2",
    ],
)

cc_binary(
    name = "cannon-host",
    srcs = glob([
        "*.cpp",
        "*.h",
    ]),
    deps = [
        "//tapa-lib:tapa",
        "@gflags",
        "@vitis_hls//:include",
    ],
)

tapa_xo(
    name = "cannon-xo",
    src = "cannon.cpp",
    hdrs = ["cannon.h"],
    include = ["."],
    platform_name = "xilinx_u250_gen3x16_xdma_4_1_202210_1",
    top_name = "Cannon",
)

vpp_xclbin(
    name = "cannon-hw-emu-xclbin",
    platform_name = "xilinx_u250_gen3x16_xdma_4_1_202210_1",
    target = "hw_emu",
    top_name = "Cannon",
    xo = ":cannon-xo",
)
